version "0.0.1-SNAPSHOT"
description = """SolarThing"""

buildscript {
    ext {
        junit5Version = '5.5.2'
        slf4jVersion = '1.7.29'
        log4jVersion = '2.12.1'
        jacksonVersion = '2.10.1'
    }
}
allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
        google()
    }
}

subprojects {
    apply plugin: 'java-library'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    repositories {
        maven { url "https://jitpack.io" }
        maven { url "https://repo.maven.apache.org/maven2" }
    }
    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit5Version
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit5Version
        implementation 'org.jetbrains:annotations:15.0'
    }
    test {
        useJUnitPlatform()
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile){
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}
project(":client"){
    apply plugin: 'java'
    dependencies {
        api project(":core")
        api project(":pvoutput")
    }
}
project(":pvoutput"){
    apply plugin: 'java'
    dependencies {
        api project(":core")
    }
}
project(":schema-generator"){
    apply plugin: 'java'
    dependencies {
        api project(":core")
    }
}
